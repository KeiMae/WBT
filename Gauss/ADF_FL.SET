/* -----------------------------------------------------------------------
  "Testing for Multiple Bubbles" by Phillips, Shi and Yu (2011)
    
    In this program, we calculate the ADF statistic with a fixed
    lag order.

  if flag=1 use ADF with constant & without trend
  if flag=2 use ADF with constant & trend
  if flag=3 use ADF without constant & trend

------------------------------------------------------------------------- */
proc ADF_FL(y,adflag,flag);
  local t1,const,trend,tval,y1,dy,dy0,i,du,dt,x,beta,
        eps,sig,k,there,t2,x1,x2,j,dy01,tvalue;

  t1=rows(y)-1; const=ones(t1,1); trend=seqa(1,1,t1);

  if flag==1; tval=zeros(t1,2*2+1); endif;
  if flag==2; tval=zeros(t1,3*2+1); endif;
  if flag==3; tval=zeros(t1,1*2+1); endif;

  y1  = y[rows(y)-t1:rows(y)-1];
  dy  = y[2:rows(y)] - y[1:rows(y)-1];
  dy0 = dy[rows(dy)-t1+1:rows(dy)];
  x=y1;


  if flag==1;                   x=x~const; endif;
  if flag==2;                   x=x~const~trend; endif;
  if flag==3;                   x=x; endif;

  x1=x;


   t2=t1-adflag;
   x2=x1[rows(x1)-t2+1:rows(x1),.];         // @-from k+1 to the end (including y1 and x)-@
   dy01=dy0[rows(dy0)-t2+1:rows(dy0)];      // @-from k+1 to the end (including dy0)-@
   
   if adflag>0;
      j=1; 
      do while j<=adflag; 
         x2=x2~dy[rows(dy)-t2+1-j:rows(dy)-j];     // @-including k lag variables of dy in x2-@
         j=j+1; 
      endo;
    endif;

   beta = inv(x2'x2)*x2'dy01;                               // @-model A-@
   eps  = dy01 - x2*beta;
   if flag==1; sig = sqrt ( diag ( eps'eps/(t2-adflag-2) * inv(x2'x2) ) ); endif;
   if flag==2; sig = sqrt ( diag ( eps'eps/(t2-adflag-3) * inv(x2'x2) ) ); endif;
   if flag==3; sig = sqrt ( diag ( eps'eps/(t2-adflag-1) * inv(x2'x2) ) ); endif;

   tvalue=beta./sig;
   retp(tvalue[1]); 
endp;
